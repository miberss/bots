on load:
    suppress variable save warnings
    delete {-npcs::*}
    loadPlayerHead("M1B3R5", "eyJ0ZXh0dXJlcyI6eyJTS0lOIjp7InVybCI6Imh0dHA6Ly90ZXh0dXJlcy5taW5lY3JhZnQubmV0L3RleHR1cmUvMTZiMTkzNjU0YzU4NGY0NDZhODM2NTUwNTMwMDE4ZmI3YTFkZDdiNWU0NzcxOGE2MTVlMWM1ZTY5NWNmMDFmYiJ9fX0=")
    loadPlayerHead("N00TRY", "eyJ0ZXh0dXJlcyI6eyJTS0lOIjp7InVybCI6Imh0dHA6Ly90ZXh0dXJlcy5taW5lY3JhZnQubmV0L3RleHR1cmUvODhhYjk3OWFiYjA2MmNkY2JkNTA3ZDliNzZkNDQ3ZTc3NzYzNDAzYTA4NzZlNjc2ZDgwNTAyNzdjMzRiNjY3NSJ9fX0=")
    loadPlayerHead("DR34M", "eyJ0ZXh0dXJlcyI6eyJTS0lOIjp7InVybCI6Imh0dHA6Ly90ZXh0dXJlcy5taW5lY3JhZnQubmV0L3RleHR1cmUvM2M5NWU0NTBmMGU1MWM0NjhlZDY2MzliNGNmMDM4ZDhmNDAxNTU0MDVjYmZiODdmZmMzZjRmNzA2MDRhODgzZSJ9fX0=")
    loadPlayerHead("3MD33", "eyJ0ZXh0dXJlcyI6eyJTS0lOIjp7InVybCI6Imh0dHA6Ly90ZXh0dXJlcy5taW5lY3JhZnQubmV0L3RleHR1cmUvZTAxOTU4NWIyYmNmYTlhYmM3OGM4N2NkMjMwYTRiNmNiMDhiMzc4MDI1NDdhNGU3YWFhMWI3MTNkOGQ3Nzc3ZCJ9fX0=")
    set {-npc_info::M1B3R5::location} to vector(9, 0, 6)
    set {-npc_info::M1B3R5::text::*} to "Day 3,948.||Status: still shooting."
    set {-npc_info::M1B3R5::color} to red # rgb(50,160,20); id like to see colors properly implemented, legacy feels bland.
    add "M1B3R5" to {-npcs::*}
    set {-npc_info::N00TRY::location} to vector(-39, 0, 25)
    set {-npc_info::N00TRY::text::*} to "What do we do||when the war ends?", "Error. Invalid question."
    set {-npc_info::N00TRY::color} to blue
    add "N00TRY" to {-npcs::*}
    set {-npc_info::DR34M::location} to vector(-17, 0, -22)
    set {-npc_info::DR34M::text::*} to "If I find one more screw||in my soup, I'm rewiring||the chef."
    set {-npc_info::DR34M::color} to yellow
    add "DR34M" to {-npcs::*}
    set {-npc_info::3MD33::location} to vector(-35, 0, -59)
    set {-npc_info::3MD33::text::*} to "Humans said we'd||make life easier...||Then they left."
    set {-npc_info::3MD33::color} to light gray
    add "3MD33" to {-npcs::*}

function create_npc_dialogue_entity(l: location, color: color, speaker: text, message: text):
    set {_speaker} to {_speaker} in title case
    set {_s} to (size of {_message} split by "||")
    replace all "||" in {_message} with "%nl%<red>  "
    if length of {_speaker} < 30:
        set {_spaces} to 30 - length of {_speaker}
        set {_speaker} to {_speaker} + (" " repeated {_spaces} times)
    spawn text display at {_l} ~ vector(-1.9, -0.4, {_y}):
        set display text alignment of entity to left aligned
        set display text background color of entity to bukkitColor(50, (red value of {_color}) / 4, (green value of {_color}) / 4, (blue value of {_color}) / 4)
        set display scale of entity to vector(1.25, 1.25, 1.25)
        set display brightness of entity to displayBrightness(15, 15)
        rotate entity by x -90, y 225, z 0
        entity.setPersistent(false)
        set {_e} to entity
    set {_sent} to ""
    set {_y} to -0.8
    set {_x} to -1.3
    loop {_message} split by "":
        if loop-value is "ยง":
            subtract 0.20 from {_y}
            subtract 0.20 from {_x}
        set display translation of {_e} to vector({_x}, -0.4, {_y})
        set {_sent} to {_sent} + loop-value
        run 1 tick later:
            set display text of {_e} to minimessage from "<%{_color}%>%{_speaker}%%nl%<#c4c4c4>  %{_sent}%"
        wait {-punctuation::%loop-value%} ? 1 tick
    wait 2 seconds
    kill {_e}

function play_npc_dialogue(l: location, npc: text):
    set {_color} to {-npc_info::%{_npc}%::color}
    loop {-npc_info::%{_npc}%::text::*}:
        set {_msg} to loop-value
        replace all "||" in {_msg} with "%nl%<#c4c4c4>  "
        set {_d} to get_delay({_msg}) + timespan(2, seconds)
        create_npc_dialogue_entity({_l}, {_color}, {_npc}, loop-value)
        wait {_d}

function spawn_map_npcs(id: integer):
    set {_center} to location({_id}*1000, 0, 3)
    loop {-npcs::*}:
        set {_t} to loop-value
        set {_loc} to {-npc_info::%{_t}%::location}
        spawn item display at {_center} ~ {_loc} ~ vector(0, 0.5, 0):
            rotate entity by x 0, y 45, z 0
            set display item of entity to head({_t})
            entity.setPersistent(false)
            set {_e} to entity
        set metadata "name" of {_e} to {_t}
        set metadata "last_spoke" of {_e} to 30 seconds ago
        add {_e} to {-map_npcs::%{_id}%::*}